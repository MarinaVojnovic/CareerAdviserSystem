//created on: 5 May 2020

import com.sbnz.career.adviser.model.*;
import com.sbnz.career.adviser.entity.*;
import java.util.List;

agenda-group "preferencesTest"

rule "Calculating number of matching activities"
	salience 200
	no-loop
	lock-on-active
	when
	
		$criteriums : Criteriums(personality == true)
		$questPos : List( size > 0) from collect (PreferenceQuestionResult(isChecked == true))
		$allPosActivities: List() from accumulate (
			$question: PreferenceQuestionResult($activity : activity) from $questPos,
			collectList($activity)
		)
		
		 $possibleProfession : PossibleProfession($activities : profession.activities, $score : score)

    	 $num:= Number(intValue >= 1) 
			from accumulate( 
				$a : Preference( this memberOf $allPosActivities)
				from $activities;
				count($a)
			)
	
	then
		System.out.println("NUMBER OF PREFRENCES SET");
		modify($possibleProfession) {setNumPref((Long) $num);}
		System.out.println($num);
end


		
		
rule "If there are not possible profession, find and insert them"
	no-loop
	lock-on-active
    when
        $criteriums : Criteriums(personality == false, preferences == true) //znaci da nije kalkulisan possibleProfession
        $questPos : List( size > 0) from collect (PreferenceQuestionResult(isChecked == true))
        
        $allPosActivities: List() from accumulate (
			$question: PreferenceQuestionResult($activity : activity) from $questPos,
			collectList($activity)
		)
		
       $profession : Profession($name : name, $activities : activities)

    	 $num:= Number(intValue >= 1) 
			from accumulate( 
				$a : Preference( this memberOf $allPosActivities)
				from $activities;
				count($a)
			)
    then
    	 System.out.println("Possible profession inserted "+$name);
    	 PossibleProfession possibleProfession = new PossibleProfession($profession);
    	 possibleProfession.setNumPref((Long) $num);
 	  	 insert(possibleProfession);
 	  	 System.out.println("Num of pref: "+$num);
 	  	// delete($profession);
 	  	 
end

//Maximum satisfied
rule "Rule 0 - preferences test"
	salience 1
	no-loop
	when
		$l : List( size > 0) from collect (PossibleProfession(numPref == profession.activities.size()))
		accumulate(
			$p : PossibleProfession (
				$k : numPref
			) from $l;
			$max: max($k)
		)
		
		$possibleProfession : PossibleProfession( numPref == $max, $name : profession.name ) from $l
	then
		 System.out.println("Rule 0 : 1.0 "+$name);
		 $possibleProfession.increaseScore(1.5);
end

//Calculating score for professions where all personality traits are satisfied
rule "Rule 1 - preferences test"
	salience 1
	no-loop
	when
		$l : List( size > 0) from collect (PossibleProfession(numPref == profession.activities.size()))
			accumulate(
				$p : PossibleProfession (
					$k : numPref
				) from $l;
				$max: max($k)
			)
		$possibleProfession : PossibleProfession(numPref == profession.activities.size(),numPref != $max, $name : profession.name)
	then
		System.out.println("Rule 1 preferences test, everything satisfied, score : 1.0 "+$name);
		$possibleProfession.increaseScore(1.0);
end



//Calculating score for professions where not all personality traits are satisfied and are bigger than max satisfied
rule "Rule 2 - preferences test"
	salience 1
	no-loop
	when
		$l : List( size > 0) from collect (PossibleProfession(numPref == profession.activities.size()))
		accumulate(
			$p : PossibleProfession (
				$k : numPref
			) from $l;
			$max: max($k)
		)
		$possibleProfession : PossibleProfession(numPref < profession.activities.size(), numPref > $max, $name : profession.name)
	
			
	then
		System.out.println("Rule 2 preferences test, not everything satisfied 1.25" + $name);
		$possibleProfession.increaseScore(1.25);
end


rule "Rule 3 - preferences test"
	salience 1
	no-loop
	when
		$l : List( size > 0) from collect (PossibleProfession(numPref == profession.activities.size()))
		accumulate(
			$p : PossibleProfession (
				$k : numPref
			) from $l;
			$min: min($k)
		)
		$possibleProfession : PossibleProfession(numPref < profession.activities.size(), numPref <= $min, $name : profession.name, $numPref : numPref, $activities : profession.activities)
	
	then
		 System.out.println("Rule 3 preferences test " + $name);
		 $possibleProfession.increaseScore((double) $numPref/$activities.size());
end

rule "Rule 5 - preferences test"
	salience 1
	no-loop
	when
		not (PossibleProfession(numPref == profession.activities.size()))
		
		$possibleProfession : PossibleProfession(numPref < profession.activities.size(), $name : profession.name, $numPref : numPref, $activities : profession.activities)
	
	then
		 System.out.println("Rule 5 preferences test " + $name);
		 $possibleProfession.increaseScore((double) $numPref/$activities.size());
end

rule "Rule 4 - preferences test"
	salience 1
	no-loop
	when
		$l : List( size > 0) from collect (PossibleProfession(numPref == profession.activities.size()))
		accumulate(
			$p : PossibleProfession (
				$k : numPref
			) from $l;
			$min: min($k),
			$max: max($k)
		)
		$possibleProfession : PossibleProfession(numPref < profession.activities.size(), numPref > $min, numPref <= $max, $name : profession.name)
		
	then
		 System.out.println("Rule 4 : 1.0" + $name);
		 $possibleProfession.increaseScore(1.0);
end


